#const
var UP_DIR = 1
var RIGHT_DIR = 2
var LEFT_DIR = 4
var LOCATION_DATA_TILE = 11
var DIRECTION_DATA_TILE = 12

#variables
var dir = 90
var good
var scan

#functions
fun TurnLeft()
    robo_left()
    dir = (dir + 120) % 360

fun MoveForward()
    robo_forward()
    dir = (dir + 300) % 360

fun LookUp()
    while dir is not 90
        TurnLeft()

fun LookLeft()
    while not (dir is 150 or dir is 210)
        TurnLeft()

fun LookRight()
    while not (dir is 30 or dir is 330)
        TurnLeft()

fun DirWalk()
    loop
        scan = robo_use()
        if scan < 1
            break
        else if scan is UP_DIR
            LookUp()
        else if scan is RIGHT_DIR
            LookRight()
        else # LEFT_DIR
            LookLeft()
        MoveForward()

#main
loop
    scan = robo_scan()
    if scan > 0 or robo_forward_location() is good
        MoveForward()
        if scan is LOCATION_DATA_TILE and not good
            good = robo_use()
        else if scan is DIRECTION_DATA_TILE
            DirWalk()
    else
        TurnLeft()
